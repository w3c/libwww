dnl Process this file with Autoconf to create configure
dnl (c) COPYRIGHT MIT 1995.
dnl Please first read the full copyright statement in the file COPYRIGH.

AC_PREREQ(2.2)
AC_INIT(Library)
AC_REVISION
AC_CONFIG_HEADER(config.h)
AC_CONFIG_AUX_DIR(config)

dnl Cheks for platform information
AC_CANONICAL_SYSTEM

dnl Initializing Automake
AM_INIT_AUTOMAKE
AC_PROG_MAKE_SET
AC_ARG_PROGRAM

dnl Variables for Automake.
PACKAGE=w3c-libwww
AC_SUBST(PACKAGE)
VERSION=5.0a
AC_SUBST(VERSION)
AC_DEFINE_UNQUOTED(W3C_VERSION, "$VERSION")

AC_PROG_CC
AC_PROG_RANLIB

dnl Remove next line and you get an error -- autoconf
dnl should deal with this automatically.
AC_ISC_POSIX
AC_PATH_X
dnl AC_PATH_XTRA 	Causes problems...

dnl Various compiler directives
case "$target" in
    alpha-dec*)
	if test "$CC" = "cc"; then
	  CFLAGS="$CFLAGS -std1"
	  AC_MSG_WARN(Adding extra compiler directives for Digital Unix)
	fi
	;;
    hp*)
	if test "$CC" = "cc"; then
	  CFLAGS="$CFLAGS -Aa -D_HPUX_SOURCE"
	  AC_MSG_WARN(Adding extra compiler directives for HT-UX)
	fi
	;;
esac

dnl Checks for configuration arguments
AC_ARG_WITH(wais, [  --with-wais             build in the WAIS searching code],
  AC_PATH_WAIS(
	[AC_DEFINE(HT_DIRECT_WAIS)
	 HTWAIS="HTWAIS.o"
	 WAISINCLUDE="${srcdir}/$ac_path_wais/include"] ,
	[AC_MSG_WARN(Cannot find WAIS; code will be built without it)
	 HTWAIS=""
	 WAISINCLUDE="" ] ),
  HTWAIS=""
  WAISINCLUDE="")
AC_SUBST(HTWAIS)
AC_SUBST(WAISINCLUDE)

AC_ARG_WITH(socks, [  --with-socks            build in the firewall-breaching SOCKS code],
  AC_PATH_SOCKS(
    AC_DEFINE(SOCKS),
    AC_MSG_WARN(Cannot find SOCKS; code will be built without it)
  ),
)

AC_ARG_ENABLE(cyrillic, [  --enable-cyrillic       build Cyrillic version],
  AC_DEFINE(CYRILLIC)
  A_STDIO="a_stdio.o", A_STDIO="")
AC_SUBST(A_STDIO)

AC_ARG_ENABLE(posix, [  --enable-posix          define _POSIX_SOURCE],
  AC_DEFINE(_POSIX_SOURCE) [CFLAGS="$CFLAGS -D_POSIX_SOURCE"],)

AC_ARG_ENABLE(reentrant, [  --enable-reentrant      define _REENTRANT to enable reentrant system calls],
  AC_DEFINE(HT_REENTRANT) [CFLAGS="$CFLAGS -D_REENTRANT"],)

dnl Checks for programs.
AC_PROG_INSTALL
AC_PROG_RANLIB

AC_CHECK_PROGS(have_telnet, telnet)
if test -n "$have_telnet" ; then
  AC_DEFINE_UNQUOTED(TELNET_PROGRAM, "$have_telnet")
fi
AC_CHECK_PROGS(have_rlogin, rlogin)
if test -n "$have_rlogin" ; then
  AC_DEFINE_UNQUOTED(RLOGIN_PROGRAM, "$have_rlogin")
fi
AC_CHECK_PROGS(have_tn3270, tn3270 x3270)
if test -n "$have_tn3270" ; then
  AC_DEFINE_UNQUOTED(TN3270_PROGRAM, "$have_tn3270")
fi

AC_PATH_PROG(RM, rm, rm)
AC_PATH_PROG(AR, ar, ar)
AC_PATH_PROG(MKDIR, mkdir, mkdir)
AC_PATH_PROG(CHMOD, chmod, chmod)
AC_PATH_PROG(CP, cp, cp)
AC_PATH_PROG(TAR, tar, tar)
AC_PATH_PROG(CVS, cvs, cvs)
AC_PATH_PROG(SED, sed, sed)
AC_PATH_PROGS(COMPRESS, gzip compress, compress)
AC_PROG_LN_S

dnl One remaining platform-specific difference in the library is the
dnl default postscript and image viewers: On the NeXT they are "open,"
dnl on other platforms they are ghostview or xv.  We should decide
dnl how to handle these things; perhaps the following lines would be
dnl useful:
dnl AC_PATH_PROGS(postscript_viewer, open ghostview, ghostview)
dnl AC_DEFINE_UNQUOTED(PRESENT_POSTSCRIPT, "$postscript_viewer %s")
dnl AC_PATH_PROGS(image_viewer, open xv, xv)
dnl AC_DEFINE_UNQUOTED(PRESENT_IMAGE, "$image_viewer %s")

dnl Checks for misc files
dnl AC_FILE_RESOLV_CONF
AC_PATH_PROG(have_resolv_conf, resolv.conf, , /etc, /usr/etc)
if test -n "$have_resolv_conf" ; then
  AC_DEFINE_UNQUOTED(RESOLV_CONF, "$have_resolv_conf")
fi

dnl Name the default location of icons
icondir=wwwicons
AC_SUBST(icondir)

dnl Checks for libraries:
AC_CHECK_LIB(ICE, IceConnectionNumber)
AC_CHECK_LIB(socket, socket)
AC_CHECK_LIB(inet, connect)
AC_CHECK_LIB(nsl, t_accept)
AC_CHECK_LIB(dl, dlopen)

dnl Checks for TcL Library
AC_PATH_TCL(
	[AC_DEFINE(TCL)
	 W3CLIB="w3clib"],
	[AC_MSG_WARN(Cannot find TCL; code will be built without it)
	 W3CLIB=""]
)
AC_SUBST(W3CLIB)
AC_SUBST(LIBTCL)

dnl Checks for header files:
AC_CHECK_HEADERS(stdio.h)
AC_CHECK_HEADERS(sys/types.h types.h)
AC_CHECK_HEADERS(sys/unistd.h unistd.h)
AC_CHECK_HEADERS(sys/fcntl.h fcntl.h)
AC_CHECK_HEADERS(sys/limits.h limits.h)
AC_CHECK_HEADERS(sys/stat.h stat.h)
AC_CHECK_HEADERS(sys/file.h)
AC_CHECK_HEADERS(sys/ioctl.h)
AC_CHECK_HEADERS(sys/time.h time.h)
AC_CHECK_HEADERS(sys/systeminfo.h)
AC_CHECK_HEADERS(string.h strings.h)
AC_CHECK_HEADERS(syslog.h)
AC_CHECK_HEADERS(sys/socket.h socket.h)
AC_CHECK_HEADERS(appkit/appkit.h appkit.h)
AC_CHECK_HEADERS(netinet/in.h in.h)
AC_CHECK_HEADERS(dn.h)
AC_CHECK_HEADERS(sys/ipc.h)
AC_CHECK_HEADERS(net/errno.h sys/errno.h errno.h)
AC_CHECK_HEADERS(pwd.h)
AC_CHECK_HEADERS(grp.h)
AC_CHECK_HEADERS(arpa/inet.h inet.h)
AC_CHECK_HEADERS(netdb.h)
AC_CHECK_HEADERS(manifest.h)
AC_CHECK_HEADERS(bsdtypes.h)
AC_CHECK_HEADERS(stdefs.h)
AC_CHECK_HEADERS(bsdtime.h)
AC_CHECK_HEADERS(sys/select.h select.h)
AC_CHECK_HEADERS(dnetdb.h)
AC_CHECK_HEADERS(libc.h)
AC_CHECK_HEADERS(stdlib.h)
AC_CHECK_HEADERS(malloc.h)
AC_CHECK_HEADERS(memory.h)
AC_CHECK_HEADERS(unixlib.h)
AC_CHECK_HEADERS(ctype.h)
AC_CHECK_HEADERS(cursesX.h curses.h)
AC_CHECK_HEADERS(sys/resource.h resource.h)
AC_HEADER_DIRENT
AC_CHECK_HEADERS(dir.h direct.h) dnl AC_HEADER_DIRENT only checks for some..
AC_HEADER_STDC
AC_HEADER_SYS_WAIT
AC_HEADER_TIME
AC_HEADER_STAT

dnl Checks for typedefs, structures, and compiler characteristics:
AC_TYPE_UID_T
AC_TYPE_PID_T
AC_TYPE_GETGROUPS
AC_TYPE_MODE_T
AC_TYPE_SIZE_T
AC_CHECK_TYPE(BOOLEAN, char)
AC_CHECK_TYPE(u_char, unsigned char)
AC_CHECK_TYPE(u_short, unsigned short)
AC_CHECK_TYPE(u_long, unsigned long)
AC_CHECK_SIZEOF(int)
AC_CHECK_SIZEOF(long)
AC_C_CONST
AC_C_BIGENDIAN
AC_C_CHAR_UNSIGNED
AC_STRUCT_TM
AC_STRUCT_GMTOFF
AC_STRUCT_TIMEZONE
AC_STRUCT_WINSIZE
AC_HEADER_TIME

dnl Other types that aren't checked for, but maybe should be:
dnl AC_TYPE_OFF_T AC_STRUCT_ST_RDEV 

dnl Checks for library functions:
AC_TYPE_SIGNAL
AC_FUNC_VPRINTF
AC_FUNC_STRFTIME
AC_CHECK_FUNCS(getcwd gethostname getdomainname getwd mktime timegm \
		select socket strerror strtol opendir getpid strchr memcpy \
		getlogin getpass fcntl readdir sysinfo ioctl chdir tempnam)
AC_CHECK_FUNC(unlink, , AC_CHECK_FUNC(remove, AC_DEFINE(unlink, remove)))

dnl Checks for external globals.
dnl AC_CHECK_EXTERNS(socket_errno sys_errlist sys_nerr)

dnl This is the standard UNIX value
AC_DEFINE(CACHE_FILE_PREFIX, "/usr/wsrc/")

dnl THINGS TO CONSIDER
dnl ******************
AC_DEFINE(TTY_IS_SELECTABLE)

dnl HT_REENTRANT
dnl POLL_PORTS
dnl NOT_ASCII
dnl ******************

dnl Create output files
AC_OUTPUT([Makefile
config/Makefile
Library/Makefile Library/User/Makefile Library/User/Patch/Makefile
Library/User/Architecture/Makefile Library/User/Using/Makefile Library/User/Guide/Makefile
Library/User/Style/Makefile Library/User/Platform/Makefile
Library/Test/Makefile Library/Test/User/Makefile Library/Test/src/Makefile
Library/src/Makefile Library/src/windows/Makefile Library/src/vms/Makefile
Library/Examples/Makefile
PICS-client/Makefile PICS-client/User/Makefile PICS-client/src/Makefile PICS-client/src/windows/Makefile
LineMode/Makefile LineMode/User/Makefile
LineMode/src/Makefile LineMode/src/windows/Makefile LineMode/src/vms/Makefile
Robot/Makefile Robot/User/Makefile Robot/src/Makefile Robot/src/windows/Makefile
MiniServ/Makefile MiniServ/User/Makefile MiniServ/src/Makefile
ComLine/Makefile ComLine/User/Makefile ComLine/src/Makefile
Listen/Makefile Listen/User/Makefile Listen/src/Makefile
Icons/Makefile Icons/WWW/Makefile Icons/32x32/Makefile Icons/internal/Makefile
], date>stamp-h)
